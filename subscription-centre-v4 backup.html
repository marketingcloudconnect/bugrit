%%[
/********************
 * GET INITIAL VARS *
 *******************/
SET @CampaignMemberID = QueryParameter('CampaignMemberID')
SET @AlternateKey = QueryParameter('AlternateKey')
SET @LeadID = QueryParameter('LeadID')
SET @ContactID = QueryParameter('ContactID')
SET @CampaignName = QueryParameter('CampaignName')
SET @Action = QueryParameter('Action')

SET @MID = memberid
IF MemberID == '7301771' THEN
  /* SMC BU */
  SET @SubscriptionListRecordTypeID = '0120I0000015Rq9QAE'
ENDIF
]%%
%%[
/* UPDATE EVENT CAMPAIGN REGISTRATION */
IF (NOT EMPTY(@CampaignMemberID) AND @Action == "Cancel registration") THEN
	SET @NewValue = QueryParameter('SetMemberStatus')
	IF EMPTY(@NewValue) THEN
		SET @NewValue = "De-registered"
	ENDIF
	SET @updated = UpdateSingleSalesforceObject("CampaignMember", @CampaignMemberID, "Status", @NewValue)
	IF(@updated) THEN
		SET @alert = "success"
		SET @UpdateMessage = "Sorry to see you go. Your cancellation has been recorded."
	ELSE
		SET @alert = "danger"
		SET @UpdateMessage = "There was a problem processing this request."
	ENDIF
ENDIF

IF (NOT EMPTY(@CampaignMemberID) AND @Action == "Undo cancellation") THEN
	SET @NewValue = QueryParameter('SetMemberStatus')
	IF EMPTY(@NewValue) THEN
		SET @NewValue = "Registered"
	ENDIF
	SET @updated = UpdateSingleSalesforceObject("CampaignMember", @CampaignMemberID, "Status", @NewValue)
	IF(@updated) THEN
		SET @alert = "success"
		SET @UpdateMessage = "Welcome back! Your re-registration has been recorded."
	ELSE
		SET @alert = "danger"
		SET @UpdateMessage = "There was a problem processing this request."
	ENDIF
ENDIF

/* UPDATE SUBSCRIPTION CAMPAIGN STATUS */
IF (NOT EMPTY(@CampaignMemberID) AND @Action == "Unsubscribe") THEN
	SET @updated = UpdateSingleSalesforceObject("CampaignMember", @CampaignMemberID, "ChannelEmail__c", "false")
	IF(@updated) THEN
		SET @alert = "success"
		SET @UpdateMessage = "You have been unsubscribed from this list. Sorry to see you go."
	ELSE
		SET @alert = "danger"
		SET @UpdateMessage = "There was a problem processing this request."
	ENDIF
ENDIF

IF (NOT EMPTY(@CampaignMemberID) AND @Action == "Subscribe") THEN
	SET @updated = UpdateSingleSalesforceObject("CampaignMember", @CampaignMemberID, "ChannelEmail__c", "true")
	IF(@updated) THEN
		SET @alert = "success"
		SET @UpdateMessage = "You are now subscribed. Welcome to the list!"
	ELSE
		SET @alert = "danger"
		SET @UpdateMessage = "There was a problem processing this request."
	ENDIF
ENDIF



/*****************************
 * UPDATE SUBSCRIPTION LISTS *
 ****************************/
IF @Action == "Save changes" THEN
	SET @crmAllSubscriptionLists = RetrieveSalesforceObjects('Campaign', 'Id,Name,PreferencePage__c,RecordTypeId,Public_List_Description__c,Public_Display_Name__c,Channel_Type__c','RecordTypeId','=',@SubscriptionListRecordTypeID,'PreferencePage__c','=','true')
	SET @listCount = rowcount(@crmAllSubscriptionLists)
	SET @subscriptionsUpdated = False
	SET @today = Format(Now(),"yyyy-MM-dd")
	]%%
	<script runat="server" type="text/javascript">
	    Platform.Load("core","1"); 
	    try {
	</script>
	%%[


	/* UPDATE SUBSCRIPTION LIST */
	FOR @i = 1 TO @listCount DO
		SET @thisSubscriptionList = ROW(@crmAllSubscriptionLists, @i)
		SET @thisSubscriptionListID = FIELD(@thisSubscriptionList, 'Id')
		SET @thisEmailUpdate = QueryParameter(CONCAT('setemail', @thisSubscriptionListID))
		SET @thisSMSUpdate = QueryParameter(CONCAT('setsms', @thisSubscriptionListID))
		SET @optedInEmail = False
		SET @optedInSMS = False





		/* Email opt-in */
		IF (NOT EMPTY(@thisEmailUpdate)) AND @thisEmailUpdate == 'true' THEN
			]%%  <h3 class="debug">EMAIL OPT IN</h3>  %%[
			]%% <div class="debug">LongSFID(@AlternateKey): %%=v(LongSFID(@AlternateKey))=%%</div> %%[
			]%% <div class="debug">thisSubscriptionListID: %%=v(@thisSubscriptionListID)=%%</div> %%[
			SET @crmListMember = ""
			SET @crmListMembers = RetrieveSalesforceObjects(
				'CampaignMember',
				'Id,ChannelEmail__c, Active__c, Email_Unsubscribed_Date__c, Email_Unsubscribed_Source__c, Email_Subscribed_Date__c, Email_Subscription_Source__c',
				'Lead_Contact_ID_18_digits__c','=',LongSFID(@AlternateKey),
				'CampaignId','=',@thisSubscriptionListID
			)
			IF rowcount(@crmListMembers) > 0 THEN
				]%% <div class="debug">Email Opt-in - CampaignMember object exists and will be updated </div> %%[
				SET @crmListMember = ROW(@crmListMembers, 1)
				]%% <div class="debug">PreUpdate Email_Subscribed_Date__c: %%=FIELD(@crmListMember, 'Email_Subscribed_Date__c')=%%</div> %%[
				]%% <div class="debug">PreUpdate Email_Unsubscribed_Source__c: %%=FIELD(@crmListMember, 'Email_Unsubscribed_Source__c')=%%</div> %%[
				SET @crmListMemberID = FIELD(@crmListMember, 'Id')
				SET @updated = UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID, 
					"ChannelEmail__c", 'true',
					"Active__c","true",
					"Email_Unsubscribed_Date__c",'1970-01-01',
					"Email_Unsubscribed_Source__c","-",
				)
				IF @updated THEN
					SET @subscriptionsUpdated = true
					IF(EMPTY(FIELD(@crmListMember, 'Email_Subscription_Source__c'))) THEN
						UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID, 
							"Email_Subscription_Source__c","Registered on preference centre",
							"Email_Subscribed_Date__c",@today
						)
					ENDIF
				ELSE
					SET @subscriptionsUpdatedError = CONCAT("Email Opt-in - Error opting in existing Subscription Member: ", @crmListMemberID) 
				ENDIF
			ELSE
				]%% <div class="debug">Email Opt-in - CampaignMember object doesn't exist and will need to be created </div> %%[
				IF Substring(@AlternateKey,0,3) == '00Q' THEN
					SET @LeadContactField = "LeadId"
				ELSEIF Substring(@AlternateKey,0,3) == '003' THEN
					SET @LeadContactField = "ContactId"
				ENDIF
				]%% <div class="debug">LeadContactField: %%=v(@LeadContactField)=%% </div> %%[
				]%% <div class="debug">AlternateKey: %%=v(@AlternateKey)=%% </div> %%[
				]%% <div class="debug">Substring(@AlternateKey,0,3): %%=Substring(@AlternateKey,0,3)=%% </div> %%[
				]%% <div class="debug">ContactID: %%=v(@ContactID)=%% </div> %%[
				]%% <div class="debug">LeadID: %%=v(@LeadID)=%% </div> %%[
				SET @CampaignMemberID = CreateSalesforceObject("CampaignMember", 6,
					@LeadContactField, @AlternateKey,
					"CampaignId",@thisSubscriptionListID,
					"ChannelEmail__c", 'true',
					"Active__c","true",
					"Email_Subscription_Source__c","Registered on preference centre",
					"Email_Subscribed_Date__c",@today
				)
				SET @subscriptionsUpdated = true
			ENDIF
		ENDIF /* End Email opt-in */




		/* SMS opt-in */
		IF (NOT EMPTY(@thisSMSUpdate)) AND @thisSMSUpdate == 'true' THEN
			]%%  <h3 class="debug">SMS OPT IN</h3>  %%[
			]%% <div class="debug">LongSFID(@AlternateKey): %%=v(LongSFID(@AlternateKey))=%%</div> %%[
			]%% <div class="debug">thisSubscriptionListID: %%=v(@thisSubscriptionListID)=%%</div> %%[
			SET @crmListMember = ""
			SET @crmListMembers = RetrieveSalesforceObjects(
				'CampaignMember',
				'Id,ChannelSMS__c, Active__c, SMS_Unsubscribed_Date__c, SMS_Unsubscribed_Source__c, SMS_Subscribed_Date__c, SMS_Subscription_Source__c',
				'Lead_Contact_ID_18_digits__c','=',LongSFID(@AlternateKey),
				'CampaignId','=',@thisSubscriptionListID
			)
			IF rowcount(@crmListMembers) > 0 THEN
				]%% <div class="debug">SMS Opt-in - CampaignMember object exists and will be updated </div> %%[
				SET @crmListMember = ROW(@crmListMembers, 1)
				]%% <div class="debug">PreUpdate SMS_Subscribed_Date__c: %%=FIELD(@crmListMember, 'SMS_Subscribed_Date__c')=%%</div> %%[
				]%% <div class="debug">PreUpdate SMS_Unsubscribed_Source__c: %%=FIELD(@crmListMember, 'SMS_Unsubscribed_Source__c')=%%</div> %%[
				SET @crmListMemberID = FIELD(@crmListMember, 'Id')
				SET @updated = UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID, 
					"ChannelSMS__c", 'true',
					"Active__c","true",
					"SMS_Unsubscribed_Date__c",'1970-01-01',
					"SMS_Unsubscribed_Source__c","-",
				)
				IF @updated THEN
					SET @subscriptionsUpdated = true
					IF(EMPTY(FIELD(@crmListMember, 'SMS_Subscription_Source__c'))) THEN
						UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID, 
							"SMS_Subscription_Source__c","Registered on preference centre",
							"SMS_Subscribed_Date__c",@today
						)
					ENDIF
				ELSE
					SET @subscriptionsUpdatedError = CONCAT("SMS Opt-in - Error opting in existing Subscription Member: ", @crmListMemberID) 
				ENDIF
			ELSE
				]%% <div class="debug">SMS Opt-in - CampaignMember object doesn't exist and will need to be created </div> %%[
				IF Substring(@AlternateKey,0,3) == '00Q' THEN
					SET @LeadContactField = "LeadId"
				ELSEIF Substring(@AlternateKey,0,3) == '003' THEN
					SET @LeadContactField = "ContactId"
				ENDIF
				]%% <div class="debug">LeadContactField: %%=v(@LeadContactField)=%% </div> %%[
				]%% <div class="debug">AlternateKey: %%=v(@AlternateKey)=%% </div> %%[
				]%% <div class="debug">Substring(@AlternateKey,0,3): %%=Substring(@AlternateKey,0,3)=%% </div> %%[
				]%% <div class="debug">ContactID: %%=v(@ContactID)=%% </div> %%[
				]%% <div class="debug">LeadID: %%=v(@LeadID)=%% </div> %%[
				SET @CampaignMemberID = CreateSalesforceObject("CampaignMember", 6,
					@LeadContactField, @AlternateKey,
					"CampaignId",@thisSubscriptionListID,
					"ChannelSMS__c", 'true',
					"Active__c","true",
					"SMS_Subscription_Source__c","Registered on preference centre",
					"SMS_Subscribed_Date__c",@today
				)
				SET @subscriptionsUpdated = true
			ENDIF
		ENDIF /* End SMS opt-in */




		/* Email opt-out */
		IF (NOT EMPTY(@thisEmailUpdate)) AND @thisEmailUpdate == 'false' THEN
			]%%  <h3 class="debug">EMAIL OPT OUT</h3>  %%[
			]%% <div class="debug">LongSFID(@AlternateKey): %%=v(LongSFID(@AlternateKey))=%%</div> %%[
			]%% <div class="debug">thisSubscriptionListID: %%=v(@thisSubscriptionListID)=%%</div> %%[
			SET @crmListMember = ""
			SET @crmListMembers = RetrieveSalesforceObjects(
				'CampaignMember',
				'Id,ChannelEmail__c, Active__c, Email_Unsubscribed_Date__c, Email_Unsubscribed_Source__c, Email_Subscribed_Date__c, Email_Subscription_Source__c',
				'Lead_Contact_ID_18_digits__c','=',LongSFID(@AlternateKey),
				'CampaignId','=',@thisSubscriptionListID
			)
			IF rowcount(@crmListMembers) > 0 THEN
				]%% <div class="debug">Email Opt-out - CampaignMember object exists and will be updated </div> %%[
				SET @crmListMember = ROW(@crmListMembers, 1)
				]%% <div class="debug">PreUpdate Email_Subscribed_Date__c: %%=FIELD(@crmListMember, 'Email_Subscribed_Date__c')=%%</div> %%[
				]%% <div class="debug">PreUpdate Email_Unsubscribed_Source__c: %%=FIELD(@crmListMember, 'Email_Unsubscribed_Source__c')=%%</div> %%[
				]%% <div class="debug">CampaignId: %%=v(@CampaignId)=%%</div> %%[
				SET @crmListMemberID = FIELD(@crmListMember, 'Id')
				SET @updated = UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID, 
					"ChannelEmail__c", 'false',
					"Email_Unsubscribed_Date__c",@today,
					"Email_Unsubscribed_Source__c","Unsubscribed on preference centre",
				)
				IF @updated THEN
					SET @subscriptionsUpdated = true
					IF EMPTY(@CampaignID) THEN
					    SET @CampaignID = QueryParameter('CampaignID')
					ENDIF
					IF NOT EMPTY(@CampaignID) THEN
						]%% <div class="debug">Email_Unsubscribed_Source_Campaign__c is beign updated - @crmListMemberID: %%=v(@crmListMemberID)=%%</div> %%[
						UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID,
							"Email_Unsubscribed_Source_Campaign__c",@CampaignID
						)
					ENDIF
				ELSE
					SET @subscriptionsUpdatedError = CONCAT("Email Opt-out - Error opting out existing Subscription Member: ", @crmListMemberID) 
				ENDIF
			ELSE
				]%% <div class="debug">Email Opt-out - CampaignMember object doesn't exist therefore has nothing to opt out of.</div> %%[
			ENDIF
		ENDIF /* End Email opt-out */





		/* SMS opt-out */
		IF (NOT EMPTY(@thisSMSUpdate)) AND @thisSMSUpdate == 'false' THEN
			]%%  <h3 class="debug">SMS OPT OUT</h3>  %%[
			]%% <div class="debug">LongSFID(@AlternateKey): %%=v(LongSFID(@AlternateKey))=%%</div> %%[
			]%% <div class="debug">thisSubscriptionListID: %%=v(@thisSubscriptionListID)=%%</div> %%[
			SET @crmListMember = ""
			SET @crmListMembers = RetrieveSalesforceObjects(
				'CampaignMember',
				'Id,ChannelSMS__c, Active__c, SMS_Unsubscribed_Date__c, SMS_Unsubscribed_Source__c, SMS_Subscribed_Date__c, SMS_Subscription_Source__c',
				'Lead_Contact_ID_18_digits__c','=',LongSFID(@AlternateKey),
				'CampaignId','=',@thisSubscriptionListID
			)
			IF rowcount(@crmListMembers) > 0 THEN
				]%% <div class="debug">SMS Opt-out - CampaignMember object exists and will be updated </div> %%[
				SET @crmListMember = ROW(@crmListMembers, 1)
				]%% <div class="debug">PreUpdate SMS_Subscribed_Date__c: %%=FIELD(@crmListMember, 'SMS_Subscribed_Date__c')=%%</div> %%[
				]%% <div class="debug">PreUpdate SMS_Unsubscribed_Source__c: %%=FIELD(@crmListMember, 'SMS_Unsubscribed_Source__c')=%%</div> %%[
				]%% <div class="debug">CampaignId: %%=v(@CampaignId)=%%</div> %%[
				SET @crmListMemberID = FIELD(@crmListMember, 'Id')
				SET @updated = UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID, 
					"ChannelSMS__c", 'false',
					"SMS_Unsubscribed_Date__c",@today,
					"SMS_Unsubscribed_Source__c","Unsubscribed on preference centre",
				)
				IF @updated THEN
					SET @subscriptionsUpdated = true
					IF EMPTY(@CampaignID) THEN
					    SET @CampaignID = QueryParameter('CampaignID')
					ENDIF
					IF NOT EMPTY(@CampaignID) THEN
						]%% <div class="debug">SMS_Unsubscribed_Source_Campaign__c is beign updated - @crmListMemberID: %%=v(@crmListMemberID)=%%</div> %%[
						UpdateSingleSalesforceObject("CampaignMember", @crmListMemberID,
							"SMS_Unsubscribed_Source_Campaign__c",@CampaignID
						)
					ENDIF
				ELSE
					SET @subscriptionsUpdatedError = CONCAT("SMS Opt-out - Error opting out existing Subscription Member: ", @crmListMemberID) 
				ENDIF
			ELSE
				]%% <div class="debug">SMS Opt-out - CampaignMember object doesn't exist therefore has nothing to opt out of.</div> %%[
			ENDIF
		ENDIF /* End SMS opt-out */
		

	NEXT @i



	


	]%%
	<script type="text/javascript" runat="server">
	    } catch (e) {
	        Write("<strong>Parsing subs error</strong><br>e: " + e);
	    }
	</script>
	%%[
ENDIF
]%%
%%[

/*****************
 * LOOKUP FIELDS *
 *****************/
IF NOT EMPTY(@CampaignMemberID) THEN
    SET @CampaignMemberID = Substring(@CampaignMemberID,1,15)
    SET @crmCampaignMembers = RetrieveSalesforceObjects('CampaignMember', 'Status,CampaignId,Lead_Contact_ID_18_digits__c,ChannelEmail__c,ChannelSMS__c', 'Id', '=', @CampaignMemberID)
    IF rowcount(@crmCampaignMembers) <= 0 THEN
        SET @error = "Campaign member not found"
    ELSE
        SET @crmCampaignMember = row(@crmCampaignMembers, 1)
		SET @CampaignID = FIELD(@crmCampaignMember, 'CampaignId')
		SET @AlternateKey = FIELD(@crmCampaignMember, 'Lead_Contact_ID_18_digits__c')
    	SET @CampaignMemberStatus = FIELD(@crmCampaignMember, 'Status')
    	SET @SubscribeEmail = FIELD(@crmCampaignMember, 'ChannelEmail__c')
    	SET @SubscribeSMS = FIELD(@crmCampaignMember, 'ChannelSMS__c')
    ENDIF
ENDIF

IF NOT EMPTY(@AlternateKey) THEN
	SET @MemberType = Substring(@AlternateKey,0,3)
	IF @MemberType == '00Q' THEN
		SET @MemberType = "Lead"
		SET @LeadID = @AlternateKey
	ELSEIF @MemberType == '003' THEN
		SET @MemberType = "Contact"            
		SET @ContactID = @AlternateKey
	ENDIF
ENDIF

IF NOT EMPTY(@LeadID) THEN
	SET @LeadID = Substring(@LeadID,1,15)
	SET @crmLeads = RetrieveSalesforceObjects('Lead', 'FirstName,LastName,Broad_area_s_of_interest__c,Email', 'Id', '=', @LeadId)
	SET @leadCount = rowcount(@crmLeads)
	IF @leadCount <= 0 THEN
		SET @error = "Lead not found"
	ELSE
		SET @AlternateKey = @LeadID
		SET @crmLead = row(@crmLeads, 1)
		SET @FirstName = FIELD(@crmLead, 'FirstName')
		SET @LastName = FIELD(@crmLead, 'LastName')
		SET @Email = FIELD(@crmLead, 'Email')
	ENDIF
ENDIF

IF NOT EMPTY(@ContactID) THEN
	SET @ContactID = Substring(@ContactID,1,15)
	SET @crmContacts = RetrieveSalesforceObjects('Contact', 'FirstName,LastName,Broad_area_s_of_interest__c,Email', 'Id', '=', @ContactId)
	SET @contactCount = rowcount(@crmContacts)
	IF @contactCount <= 0 THEN
		SET @error = "Contact not found"
	ELSE
		SET @AlternateKey = @ContactID
		SET @crmContact = ROW(@crmContacts, 1)
		SET @FirstName = FIELD(@crmContact, 'FirstName')
		SET @LastName = FIELD(@crmContact, 'LastName')
		SET @Email = FIELD(@crmContact, 'Email')
	ENDIF
ENDIF

IF NOT EMPTY(@CampaignID) THEN
	SET @crmCampaigns = RetrieveSalesforceObjects('Campaign', 'Name,Public_Display_Name__c,RecordTypeId', 'Id', '=', @CampaignId)
	SET @campaignCount = rowcount(@crmCampaigns)
	IF @campaignCount <= 0 THEN
		SET @error = "Campaign not found"
	ELSE
		SET @crmCampaign = Row(@crmCampaigns, 1)
		SET @CampaignName = Field(@crmCampaign, 'Name')
		SET @CampaignPublicName = Field(@crmCampaign, 'Public_Display_Name__c')
		SET @RecordTypeId = FIELD(@crmCampaign, 'RecordTypeId')
		IF EMPTY(@CampaignPublicName) AND NOT EMPTY(@CampaignName) THEN
			SET @CampaignPublicName = @CampaignName
		ENDIF
	ENDIF
ENDIF
]%%
<link rel="stylesheet" type="text/css" media="screen" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/css/bootstrap.min.css">
<link rel="stylesheet" type="text/css" media="screen" href="https://maxcdn.bootstrapcdn.com/css/ie10-viewport-bug-workaround.css">
<link rel="stylesheet" type="text/css" media="screen" href="https://fonts.googleapis.com/css?family=Roboto+Condensed">
<link rel="shortcut icon" type="image/ico" href="http://www.monash.edu/favicon.ico" />
<style>
  body {
   /*font-family: 'Roboto Condensed', sans-serif;*/
   font-size: 14px;
   color: #505050;
   font-family: Helvetica,Arial,sans-serif;
  }
  h1 {
   color: #006dae;
   font-size: 1.75em;
   font-size: 42px;
   font-family: 'Century Schoolbook';
  }
  h2 {
   color: #006dae;
   font-family: 'Century Schoolbook'; 
   font-size: 33px;
  }
  .hide {
   display: none !important;
  }
  .btn {
   border-radius: 0;
  }
  .navigation-bar {
   background: #f6f6f6;
   font-size: 18px;
  }
  .navigation-link {
   text-transform: uppercase;
   padding: 10px 20px;
  }
  .outer {
   border: 1px solid #f1f1f1;
  }
  .banner {
   padding: 30px 20px;
  }
  .text {
   padding: 40px 20px;
  }
  .text-link {
   color: #494949;
  }
  .footer {
   background: #f6f6f6;
  }
  .footer-area {
   padding: 40px 20px;
   /*font-size: 0.65em;*/
   font-family: 'Helvetica Neue', HelveticaNeue, Helvetica, Arial, sans-serif;
  }
  .navigation-bar { font-family: 'Roboto Condensed'; }
  .monash-button { background-color: #006dae; color: #ffffff; border: 0; border-radius: 0; padding: 10px 30px; }
  .monash-button-save-changes { margin: 30px 0; }
  .campaign-wrapper { margin-top: 50px; margin-bottom: 50px; }
  .campaign-wrapper .alert { border-radius: 0; }
  .campaign-box {  background-color: #f6f6f6; padding: 30px; border-bottom: 1px solid #006dae;}
  .campaign-box h2 { margin-top: 0; font-size: 24px; }
  .campaign-box p { margin-top: 15px; }
  .campaign-box form { margin: 0; }
  .campaign-box .monash-button { margin-top: 15px; }

  .subscription-wrapper { margin-top: 50px; margin-bottom: 50px; }
  .subscription-wrapper .alert { border-radius: 0; }
  .subscription-channel {  }
  .sub-list-option label { background-color: #f5f5f5; padding: 10px 15px; display:block; }
  .well th { background-color: #DDDDDD; }

  .subscription-list-wrapper { margin: 15px 0; }
  .alert-subscriptions { margin: 30px 0; border-radius: 0; }
  .debug { display:block; display:none;  }
  .person-details { font-size: 120%; margin: 0em 0 0 0; }
 </style>
<div class="container">
 <div class="row">
  <div class="banner">
   <img src="//www.monash.edu/__data/assets/git_bridge/0006/509343/deploy/mysource_files/monash-logo-mono.svg" alt="Monash University" style="max-width: 270px;">
  </div>
 </div>
</div>
<div class="container-fluid navigation-bar">
 <div class="container">
  <div class="row">
   <div class="col-lg-12 navigation-link">
    <a href="http://www.monash.edu/" class="text-link">Home</a>
   </div>
  </div>
 </div>
</div>
<div class="container subscription-wrapper">
<div class="row">
<div class="col-md-12">
<h1>Preference centre</h1>
<p class="person-details"><strong>%%=v(@FirstName)=%% %%=v(@LastName)=%%</strong> | %%=v(@Email)=%%</p>
</div>
</div>
</div>
<!-- CAMPAIGN DEREG -->
<!--
@CampaignMemberStatus = %%=v(@CampaignMemberStatus)=%%<br/>
@RecordTypeId = %%=v(@RecordTypeId)=%%<br/>
@SubscriptionListRecordTypeID = %%=v(@SubscriptionListRecordTypeID)=%%<br/>
-->


%%[ IF @RecordTypeId != @SubscriptionListRecordTypeID AND (@CampaignMemberStatus == 'Registered' OR @CampaignMemberStatus == 'De-registered') THEN ]%%

<div class="container campaign-wrapper">

  %%[ IF NOT EMPTY(@UpdateMessage) THEN ]%%
    <div class="row">
      <div class="col-lg-12">
        <div class="alert alert-%%=v(@alert)=%%" role="alert">
          %%=v(@UpdateMessage)=%%
        </div>
      </div>
    </div>
  %%[ ENDIF ]%%

  <div class="row">
    <div class="col-lg-12">
      <div class="campaign-box">
        <h2>%%=v(@CampaignPublicName)=%%</h2>

        <form action="#" method="post">
          <input type="hidden" name="CampaignMemberID" value="%%=v(@CampaignMemberID)=%%">
          %%[ IF @CampaignMemberStatus == "De-registered" THEN ]%%
            <p>You are not currently registered. If you have any questions, please contact us on <a href="tel:1800666274">1800 MONASH (1800 666 274)</a>.</p>       
            <!-- input type="hidden" name="SetMemberStatus" value="Registered" -->
            <input type="submit" name="Action" value="Undo cancellation" class="monash-button" />
          %%[ ENDIF ]%%
          %%[ IF @CampaignMemberStatus == "Registered" THEN ]%%
            <p>We understand that sometimes plans change, so if you can no longer attend this event, please cancel your registration.</p>
            <!-- input type="hidden" name="SetMemberStatus" value="De-registered" -->
            <input type="submit" name="Action" value="Cancel registration" class="monash-button monash-button-dereg" />
          %%[ ENDIF ]%%
        </form>
      </div> 
    </div>
  </div>
</div>

%%[ ENDIF ]%%
<!-- END CAMPAIGN DEREG -->
<!-- CAMPAIGN DEREG -->
<!--
@SubscribeEmail = %%=v(@SubscribeEmail)=%%<br/>
@RecordTypeId = %%=v(@RecordTypeId)=%%<br/>
@SubscriptionListRecordTypeID = %%=v(@SubscriptionListRecordTypeID)=%%<br/>
-->
%%[ IF @RecordTypeId == @SubscriptionListRecordTypeID THEN ]%%

<div class="container campaign-wrapper">

  %%[ IF NOT EMPTY(@UpdateMessage) THEN ]%%
    <div class="row">
      <div class="col-lg-12">
        <div class="alert alert-%%=v(@alert)=%%" role="alert">
          %%=v(@UpdateMessage)=%%
        </div>
      </div>
    </div>
  %%[ ENDIF ]%%

  <div class="row">
    <div class="col-lg-12">
      <div class="campaign-box">
        <h2>%%=v(@CampaignPublicName)=%%</h2>

        <form action="#" method="post">
          <input type="hidden" name="CampaignMemberID" value="%%=v(@CampaignMemberID)=%%">
          %%[ IF @SubscribeEmail == "true" THEN ]%%
            <p>You are subscribed to this list. Unsubscribe to opt-out.</p>     
            <input type="submit" name="Action" value="Unsubscribe" class="monash-button" />
          %%[ ENDIF ]%%
          %%[ IF @SubscribeEmail == "false" THEN  ]%%
            <p>You are not currently subscribed to this list. Subscribe to opt-in.</p>
            <input type="submit" name="Action" value="Subscribe" class="monash-button" />
          %%[ ENDIF ]%%
        </form>
      </div> 
    </div>
  </div>
</div>

%%[ ENDIF ]%%
<!-- END CAMPAIGN DEREG -->
<!-- UPDATE MY SUBSCRIPTIONS -->
%%[
    SET @crmAllSubscriptionLists = RetrieveSalesforceObjects('Campaign', 'Id,Name,PreferencePage__c,RecordTypeId,Public_List_Description__c,Public_Display_Name__c,Channel_Type__c','RecordTypeId','=',@SubscriptionListRecordTypeID,'PreferencePage__c','=','true')
    SET @listCount = rowcount(@crmAllSubscriptionLists)
]%%
%%[ IF NOT EMPTY(@AlternateKey) THEN ]%%

<hr/>
<div class="container">
  <div class="row">
    <div class="col-lg-12">
        <h2>Update my subscriptions</h2>
    </div>
  </div>

  %%[ IF(@subscriptionsUpdated) THEN ]%%
      <div class="row">
        <div class="col-lg-12">
            <div class="alert alert-success alert-subscriptions" role="alert">
                Your subscriptions have been updated.
            </div>
        </div>
      </div>  
  %%[ ENDIF ]%%

  %%[ IF NOT EMPTY(@subscriptionsUpdatedError) THEN ]%%
      <div class="row">
        <div class="col-lg-12">
            <div class="alert alert-danger alert-subscriptions" role="alert">
                %%=v(@subscriptionsUpdatedError)=%%
            </div>
        </div>
      </div>  
  %%[ ENDIF ]%%

  <form method="GET">
    <div class="row">
        %%[ FOR @i = 1 TO @listCount DO ]%%
            <script runat="server" type="text/javascript">
                Platform.Load("core","1"); 
                try {
            </script>
            %%[
                SET @x = ADD(SUBTRACT(@listCount,@i),1)
                SET @thisSubscriptionList = ROW(@crmAllSubscriptionLists, @x)
                SET @thisSubscriptionListID = FIELD(@thisSubscriptionList, 'Id')
                SET @thisSubscriptionListName = FIELD(@thisSubscriptionList, 'Name')
                SET @thisSubscriptionListDescription = FIELD(@thisSubscriptionList, 'Public_list_description__c')
                SET @allowedChannels = FIELD(@thisSubscriptionList, 'Channel_type__c')
                SET @crmListMembers = RetrieveSalesforceObjects('CampaignMember', 'Id,ChannelEmail__c,ChannelSMS__c','Lead_Contact_ID_18_digits__c','=',LongSFID(@AlternateKey),'CampaignId','=',@thisSubscriptionListID)
                SET @crmListMember = ""

                ]%% 
                    <div class="subscription-list-wrapper col-md-12">
                    <h3>%%=v(@thisSubscriptionListName)=%%</h3>
                    <p>%%=v(@thisSubscriptionListDescription)=%%</p>
                %%[

                SET @optedInEmail = False
                SET @optedInSMS = False
                SET @optedInEmailChecked = ""
                SET @optedInSMSChecked = ""
                IF rowcount(@crmListMembers) > 0 THEN
                    SET @crmListMember = ROW(@crmListMembers, 1)
                    SET @crmListMemberID = FIELD(@crmListMember, 'Id')

                    SET @optedInEmail = FIELD(@crmListMember, 'ChannelEmail__c')
                    IF @optedInEmail == True OR @optedInEmail == "true" THEN
                        SET @optedInEmailChecked = "checked"
                    ENDIF

                    SET @optedInSMS = FIELD(@crmListMember, 'ChannelSMS__c')
                    IF @optedInSMS == True OR @optedInSMS == "true" THEN
                        SET @optedInSMSChecked = "checked"
                    ENDIF
                ENDIF
            ]%%
                    <!-- <div> %%=v(@ChannelType)=%% </div>
                    <div> rowCount(@crmListMember): %%=rowcount(@crmListMembers)=%% </div>
                    <div> subscriptionListName: %%=v(@thisSubscriptionListName)=%% </div>
                    <div> allowedChannels: %%=v(@allowedChannels)=%% (%%=IndexOf(@allowedChannels,"Email")=%%)</div>
                    %%[ /* SET @allowedChannels = "Email" */ ]%%
                    <div> AlternateKey: %%=v(@AlternateKey)=%% </div>
                    <div> subscriptionListID: %%=v(@thisSubscriptionListID)=%% ( %%=FIELD(@thisSubscriptionList, 'Id')=%% )</div>
                    <div> Email: %%=v(@optedInEmail)=%% (%%=v(@optedInEmail)=%%) </div>
                    <div> SMS: %%=v(@optedInSMS)=%% </div> -->

                    <div class="row">
                        %%[ IF IndexOf(@allowedChannels,"Email") > 0 THEN ]%%
                            <div class="col-md-4 subscription-channel">
                                <input type="hidden" id="listemail%%=v(@thisSubscriptionListID)=%%" name="" value="" />
                                <div class="sub-list-option sub-list-option-email"><label>
                                    <input type="checkbox" name="" id="" onchange="listToggle('listemail%%=v(@thisSubscriptionListID)=%%',this)" %%=v(@optedInEmailChecked)=%%>
                                    Subscribe to Email
                                </label></div>
                            </div>
                        %%[ ENDIF ]%%

                        %%[ IF IndexOf(@allowedChannels,"SMS") > 0 THEN ]%%
                            <div class="col-md-4 subscription-channel">
                                <input type="hidden" id="listsms%%=v(@thisSubscriptionListID)=%%" name="" value="" />
                                <div class="sub-list-option sub-list-option-sms"><label>
                                    <input type="checkbox" name="" id="" onchange="listToggle('listsms%%=v(@thisSubscriptionListID)=%%',this)" %%=v(@optedInSMSChecked)=%%>
                                    Subscribe to SMS
                                </label></div>
                            </div>
                        %%[ ENDIF ]%%
                    </div>
                    <script type="text/javascript">
                        function listToggle(id, checkbox) {
                            var hiddenInput = document.getElementById(id);
                            hiddenInput.setAttribute("name","set" + id.replace(/^list/,''));
                            hiddenInput.setAttribute("value", checkbox.checked);
                            console.log(hiddenInput, checkbox);
                        }
                    </script>


            <script type="text/javascript" runat="server">
                } catch (e) {
                    Write("<br>e: " + e);
                }
            </script>
                    </div> <!-- end .subscription-list-wrapper -->
        %%[ NEXT @i ]%%
    </div><!-- end .row -->
     <div class="row">
        <div class="col-md-12">
            <input type="hidden" name="AlternateKey" value="%%=v(@AlternateKey)=%%" />
            %%[ IF NOT EMPTY(@CampaignMemberID) THEN ]%%
                <input type="hidden" name="CampaignMemberID" value="%%=v(@CampaignMemberID)=%%" />
            %%[ ENDIF ]%%
            %%[ IF NOT EMPTY(@CampaignID) THEN ]%%
                <input type="hidden" name="CampaignID" value="%%=v(@CampaignID)=%%" />
            %%[ ENDIF ]%%
            <p><input type="submit" name="action" value="Save changes" class="monash-button monash-button-save-changes" /></p>
        </div>
    </div>   
  </form>
</div>



%%[ ENDIF ]%%
<!-- END UPDATE MY SUBSCRIPTIONS -->
 <hr class="debug" />
 <div class="container debug">
  <div class="row">
  <div class="col-lg-12">
    <div class="well" style="margin-top:30px;">
    <h3>DEBUG</h3>
      <table border="1">


      %%[ IF NOT EMPTY(@error) THEN ]%%
        <tr><th colspan="2" style="background-color:#800000; color: #ffffff">%%=v(@error)=%%</th></tr>
      %%[ ENDIF ]%%
      %%[ IF NOT EMPTY(@UpdateMessage) THEN ]%%
        <tr><th colspan="2" style="background-color:#000080; color: #ffffff">%%=v(@UpdateStatus)=%%</th></tr>
      %%[ ENDIF ]%%  

      

      %%[ IF NOT EMPTY(@CampaignMemberID) THEN ]%%
        <tr><th>CampaignMember</th><th>%%=v(@cmCount)=%% rows</th></tr>
        <tr><td>@CampaignMemberID</td><td>%%=v(@CampaignMemberID)=%%</td></tr>
        <tr><td>@CampaignMemberStatus</td><td>%%=v(@MemberStatus)=%%</td></tr>
        <tr><td>@CampaignID</td><td>%%=v(@CampaignID)=%%</td></tr>
        <tr><td>@MemberID</td><td>%%=v(@MemberID)=%%</td></tr>
        <tr><td>@MemberType</td><td>%%=v(@MemberType)=%%</td></tr>
        <tr><td>@LeadID</td><td>%%=v(@LeadID)=%%</td></tr>
        <tr><td>@ContactID</td><td>%%=v(@ContactID)=%%</td></tr>
      %%[ ENDIF ]%%


      %%[ IF NOT EMPTY(@SubscriptionName) THEN ]%%
        <tr><th>SubscriptionMember</th><th>%%=v(@smCount)=%% rows</th></tr>
        <tr><td>@MemberID</td><td>%%=v(@MemberID)=%%</td></tr>
        <tr><td>@LeadID</td><td>%%=v(@LeadID)=%%</td></tr>
        <tr><td>@ContactID</td><td>%%=v(@ContactID)=%%</td></tr>
        <tr><td>@SubscribeEmail</td><td>%%=v(@SubscribeEmail)=%%</td></tr>
        <tr><td>@SubscribeSMS</td><td>%%=v(@SubscribeSMS)=%%</td></tr>
        <tr><td>@SubscriptionListID</td><td>%%=v(@SubscriptionListID)=%%</td></tr>
      %%[ ENDIF ]%%


      %%[ IF NOT EMPTY(@SubscriptionListID) THEN ]%%
        <tr><th>SubscriptionList</th><th>%%=v(@slCount)=%% rows</th></tr>
        <tr><td>@SubscriptionName</td><td>%%=v(@SubscriptionName)=%%</td></tr> 
        <tr><td>@SubscriptionListName</td><td>%%=v(@SubscriptionListName)=%%</td></tr>
      %%[ ENDIF ]%%


      %%[ IF NOT EMPTY(@LeadID) THEN ]%%
        <tr><th>Lead</th><th>%%=v(@leadCount)=%% rows</th></tr>
        <tr><td>@LeadID</td><td>%%=v(@LeadID)=%%</td></tr>
        <tr><td>@FirstName</td><td>%%=v(@FirstName)=%%</td></tr>
        <tr><td>@LastName</td><td>%%=v(@LastName)=%%</td></tr>
        <tr><td>@AreasOfInterest</td><td>%%=v(@AreasOfInterest)=%%</td></tr>
      %%[ ENDIF ]%%


      %%[ IF NOT EMPTY(@ContactID) THEN ]%%
        <tr><th>Contact</th><th>%%=v(@contactCount)=%% rows</th></tr>
        <tr><td>@ContactID</td><td>%%=v(@ContactID)=%%</td></tr>
        <tr><td>@FirstName</td><td>%%=v(@FirstName)=%%</td></tr>
        <tr><td>@LastName</td><td>%%=v(@LastName)=%%</td></tr>
        <tr><td>@AreasOfInterest</td><td>%%=v(@AreasOfInterest)=%%</td></tr>
      %%[ ENDIF ]%%


      %%[ IF NOT EMPTY(@CampaignID) THEN ]%%
        <tr><th>Campaign</th><th>%%=v(@campaignCount)=%% rows</th></tr>
        <tr><td>@CampaignName</td><td>%%=v(@CampaignName)=%%</td></tr>
        <tr><td>@CampaignPublicName</td><td>%%=v(@CampaignPublicName)=%%</td></tr>
      %%[ ENDIF ]%%


      </table>



MID: %%MemberID%%<br/>
@SubscriptionListRecordTypeID: %%=v(@SubscriptionListRecordTypeID)=%%<br/>
@CampaignMemberID: %%=v(@CampaignMemberID)=%%<br/>
@LeadID: %%=v(@LeadID)=%%<br/>
@ContactID: %%=v(@ContactID)=%%<br/>
@CampaignName: %%=v(@CampaignName)=%%<br/>
@Action: %%=v(@Action)=%%<br/>



    </div>
  </div>
  </div>
</div>
<div class="container-fluid footer">
 <div class="container">
  <div class="row">
   <div class="col-lg-10 footer-area">
    Authorised by: Director, Office of Strategic Marketing and Communications. Maintained by: <a href="mailto:its-servicedesk@monash.edu" class="text-link">eSolutions ServiceDesk</a>
    <br /><br />
    Copyright &copy; 2017 Monash University. ABN 12 377 614 012 <a href="http://www.monash.edu/accessibility">Accessibility</a> - <a href="http://www.monash.edu/disclaimer-copyright">Disclaimer and copyright</a> - <a href="http://www.monash.edu/privacy">Privacy</a>, Monash University CRICOS Provider Number: 00008C, Monash College CRICOS Provider Number: 01857J. Monash University is a registered higher education provider under the TEQSA Act 2011.
    <br /><br />
    We acknowledge and pay respects to the Elders and Traditional Owners of the land on which our five Australian campuses stand. <a href="http://www.monash.edu/about/indigenous" class="text-link">Information for Indigenous Australians</a>
   </div>
  </div>
 </div>
</div>
<script src="https://code.jquery.com/jquery-3.2.1.min.js" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" crossorigin="anonymous"></script>
<script type="text/javascript" src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/js/bootstrap.min.js"></script>